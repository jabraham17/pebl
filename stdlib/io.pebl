
#
# header
#
type File = {fp:void*;}
func openFile(name:string, mode:string):File*;
func fileIsOpen(file:File*):bool;
func closeFile(file:File*):void;
func getChar(file:File*):char;
func putChar(file:File*, c:char):void;
func putChars(file:File*, c:char*, n:int):void;
func putString(file:File*, s:string):void;
func print(s: string): void;

func getFilePos(file:File*): int;
func SEEK_SET(): int;
func SEEK_CUR(): int;
func SEEK_END(): int;
func setFilePos(file:File*, offset:int, seek_type:int):void;

func getStdout(): File*;
func getStdin(): File*;

func EOF(): char;

func pathExists(path: string):bool;
func pathIsFile(path: string):bool;

#
# includes
#
func allocate(n: int):void*;
func deallocate(p: void*):void;


#
# externs
#
extern func c_openFilePointer(name:string, mode:string):void*;
extern func c_closeFilePointer(fp:void*):void;
extern func c_getChar(fp:void*):char;
extern func c_putChar(fp:void*, c:char):void;
extern func c_getStdout():void*;
extern func c_getStdin():void*;

extern func c_ftell(fp:void*):int;
extern func c_fseek(fp:void*, offset:int,seek_type:int):void;
extern func c_fseek_seek_type(seek_type:int):int;

extern func c_pathExists(path:string):int;
extern func c_pathIsFile(path:string):int;

#
# defs
#
func openFile(name:string, mode:string):File* {
  let file:File* = allocate(sizeof(File));
  file->fp = c_openFilePointer(name, mode);
  return file;
}

func fileIsOpen(file:File*):bool {
  return file->fp != 0;
}

func closeFile(file:File*):void {
  c_closeFilePointer(file->fp);
  deallocate(file);
}

func getChar(file:File*):char {
  let c:char = c_getChar(file->fp);
  return c;
}

func putChar(file:File*, c:char):void {
  c_putChar(file->fp, c);
}

func putChars(file:File*, c:char*, n:int):void {
  let i: int = 0;
  while i < n {
    putChar(file, *(c+i));
    i=i+1;
  }
}

func putString(file:File*, s:string):void {
  while ((*s):int) != 0 {
    putChar(file, *s);
    s = s + 1;
  }
}

func print(s:string):void {
  putString(getStdout(), s);
}


func getFilePos(file:File*): int {
  return c_ftell(file->fp);
}
func SEEK_SET(): int { return c_fseek_seek_type(0); }
func SEEK_CUR(): int { return c_fseek_seek_type(1); }
func SEEK_END(): int { return c_fseek_seek_type(2); }
func setFilePos(file:File*, offset:int, seek_type:int):void {
  c_fseek(file->fp, offset, seek_type);
}

type FILEPTR = File*;
let stdout: File* = 0:FILEPTR;
let stdin: File* = 0:FILEPTR;
func getStdout(): File* {
  if !stdout {
    stdout = allocate(sizeof(File));
    stdout->fp = c_getStdout();
  }
  return stdout;
}
func getStdin(): File* {
  if !stdin {
    stdin = allocate(sizeof(File));
    stdin->fp = c_getStdin();
  }
  return stdin;
}


func EOF(): char {
  return 255:char;
}


func pathExists(path: string):bool {
  return c_pathExists(path):bool;
}
func pathIsFile(path: string):bool {
  return c_pathIsFile(path):bool;
}
